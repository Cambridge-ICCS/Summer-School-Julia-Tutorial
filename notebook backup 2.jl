### A Pluto.jl notebook ###
# v0.19.40

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local iv = try Base.loaded_modules[Base.PkgId(Base.UUID("6e696c72-6542-2067-7265-42206c756150"), "AbstractPlutoDingetjes")].Bonds.initial_value catch; b -> missing; end
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : iv(el)
        el
    end
end

# â•”â•â•¡ 32fbc94f-710f-435f-91a8-13c07b9671f0
begin
	using LinearAlgebra
	using StaticArrays
	using SymPy
	using Plots
	using Images
	using PlutoUI
	using Symbolics
	using BenchmarkTools
	using PartialFunctions
	using OffsetArrays
	using ForwardDiff
	using OrdinaryDiffEq
	using Statistics
	using Markdown
end

# â•”â•â•¡ c147a9be-8ce8-4df8-a1f1-1fc6c05a73cc
begin
	using AbstractTrees
	AbstractTrees.children(d::DataType) = subtypes(d)
	print_tree(Number)
end

# â•”â•â•¡ 810898ac-546f-4832-9922-8eb61f154325
begin
	using MacroTools: combinedef, splitarg, splitdef
	
	macro memoize(expr)
		# parse the function definition into a Dict
	    def_dict = splitdef(expr)
		fname = def_dict[:name]  # name of the function
		args = map(splitarg, def_dict[:args])  # parse function arguments
	    vars = Tuple(name for (name, typ, is_splat, default) in args)

		# rename the original function definition
	    def_dict_unmemoized = copy(def_dict)
	    f = def_dict_unmemoized[:name] = Symbol("#", fname, "_orig")

		# initialize the cache and define it as a global const variable
	    cache = Dict()
	    fcachename = Symbol("#", fname, "_cache")
		mod = __module__
		fcache = isdefined(mod, fcachename) ?
             getfield(mod, fcachename) :
             Core.eval(mod, :(const $fcachename = $cache))
	
		# modify the function definition to make it use caching
		def_dict[:body] = quote
			try
				$fcache[$(vars...)]
			catch e
				if e isa KeyError
					$fcache[$(vars...)] = $f($(vars...))
				else
					throw(e)
				end
			end
	    end

		# return the modified Julia expression
	    esc(quote
	        $(combinedef(def_dict_unmemoized))  # define the original function
	        $(combinedef(def_dict))  # define the cached function
	    end)
	end
end

# â•”â•â•¡ dd861e69-2c98-49e1-9970-f5e898048d04
begin
	using KernelAbstractions

	@kernel function estimate_pi_kernel(a, n)
		i = @index(Global)
		k = prod(@groupsize())
		@inbounds a[i] = prod((i Ã· 2 * 2) / ((i-1) Ã· 2 * 2 + 1) for i in 1+i:k:n)
	end

	device = CPU()  # GPU()

	@time let N = 300_000_000, K = 64
		A = ones(K)
		run! = estimate_pi_kernel(device, K)
		run!(A, N, ndrange=size(A))
		synchronize(device)
		2 * prod(A)
	end
end

# â•”â•â•¡ cd2ebd41-45d3-4dd0-aba9-3f42070663b4
begin
	using CSV, DataFrames, NaNStatistics, DifferentialEquations
	import PlotlyJS, ModelingToolkit
	import ModelingToolkit: Differential, ODESystem
	plotlyjs()
end

# â•”â•â•¡ 135b50a4-66c1-49c5-9865-63d4311ba694
Complex{Float64}

# â•”â•â•¡ 65b87a6f-1edd-4b48-9878-90dc112881e6
subtypes(Integer), supertypes(Integer)

# â•”â•â•¡ 0858a0ec-bbf0-4d04-8755-f88838272822
@which 2im

# â•”â•â•¡ 369536ba-ffdc-49fc-9acf-1572300714be
methods(!)

# â•”â•â•¡ a1f21125-0a64-45cc-833a-801d0539892b
names(Statistics)

# â•”â•â•¡ 6ff63d76-7fd6-47fd-9c0e-8a72967a77a2
[1 2 3
 5 6 4
 9 7 8]  # or [1 2 3; 5 6 4; 9 7 8]

# â•”â•â•¡ 9fd0358b-47e1-4e19-b446-3f8c91769d1c
zeros(3, 3)

# â•”â•â•¡ 836b97b4-5350-4d85-bf60-17e52d6f9ef0
let B = @show similar(A)
	fill!(B, 3)
end

# â•”â•â•¡ b618ae29-ed57-48fc-8102-1767a51a5b7a
factorial(5)

# â•”â•â•¡ b6b5be30-5443-4709-9a24-9608b17f20f2
factorial(32)

# â•”â•â•¡ eebaac1a-1b99-44e5-b9c0-1a3ed1ea8c0b
let
	function factorial(n)
		if !(n isa Integer)
			throw(ValueError("input is not an integer"))
		elseif n < 0
			throw(ValueError("input cannot be negative"))
		else
			prod(big(1):big(n))
		end
	end
	@time factorial.(0:32)
end

# â•”â•â•¡ 8ab3b64e-e1e4-4cdf-9385-9573ad4d5863
begin
	âŸ¹(p::Bool, q::Bool) = !p | q  # \implies
	âŸº(p::Bool, q::Bool) = (p âŸ¹ q) & (q âŸ¹ p)  # \iff
	bools = Set([true, false])
	# equivalence of contrapositive statements
	all((p âŸ¹ q) âŸº (!q âŸ¹ !p) for p âˆˆ bools, q âˆˆ bools)
	# see https://github.com/JuliaLang/julia/blob/master/src/julia-parser.scm for the symbols that can be defined as infix binary operators
end

# â•”â•â•¡ 321c78d1-d175-43de-8069-cdf7cedaf20c
@time fib(32)

# â•”â•â•¡ 76b59e6b-b1b9-49e3-8fea-fc8a15238c99
begin
	@memoize mfib(n) = n < 2 ? big(n) : mfib(n-2) + mfib(n-1)
	@time @show fib.(1:32)
	@time @show mfib.(1:32)
end

# â•”â•â•¡ 1d7dce85-6652-4743-9ecc-46145a00bc70
macroexpand(@__MODULE__, :(@memoize mfib(n) = n < 2 ? big(n) : mfib(n-2) + mfib(n-1)))

# â•”â•â•¡ 4585ea88-51e7-42c6-a014-e8097db09695
function fastfib(n)
    z = BigInt()
    ccall((:__gmpz_fib_ui, :libgmp), Cvoid, (Ref{BigInt}, Culong), z, n)
    return z
end

# â•”â•â•¡ 11834ef0-f9a4-49a5-92ff-4ffe2c217912
@time fastfib(32)

# â•”â•â•¡ 9a379335-9430-4fa2-9b84-15e192ace090
@time estimate_pi_mc(300_000_000)

# â•”â•â•¡ 34d06f6b-5e8e-4823-98e7-e3707feb528d
let task = Threads.@spawn estimate_pi_mc()
	@show task
	fetch(task)
end

# â•”â•â•¡ 697828f5-2e3b-4c25-86e0-cc97cfdb8432
@time let N = 300_000_000, k = @show Threads.nthreads()
	mean(fetch.(Threads.@spawn estimate_pi_mc(NÃ·k) for _ in 1:k))
end

# â•”â•â•¡ 57f8919a-aadd-4e3b-9a24-cdca7870caa9
@time let N = 300_000_000
	# generator (laze evaluation)
	fracs = ((i Ã· 2 * 2) / ((i-1) Ã· 2 * 2 + 1) for i in 2:N)
	# fracs = [(i Ã· 2 * 2) / ((i-1) Ã· 2 * 2 + 1) for i in 2:N]
	2 * prod(fracs)
end

# â•”â•â•¡ b700e215-3ba4-45e7-bea2-89f6f7ff73f7
# Task: implement multi-threaded version

# â•”â•â•¡ 37396c37-0291-4ce6-92c6-defb547f94f5
@time nfold(x -> sin(x) + x, 5)(1)

# â•”â•â•¡ 4cd08b52-0848-458a-8b92-9909015d8edd
img = let url = "https://images.fineartamerica.com/images-medium-large-5/1-earth-from-space-kevin-a-horganscience-photo-library.jpg"
	load(@show download(url))
end

# â•”â•â•¡ f0edaa1c-cd6d-4722-9d3f-0f6328095c48
typeof(img)

# â•”â•â•¡ 6b313419-288a-4f20-8453-e61fa463d225
SVD_results = [svd(f.(img)) for f in [red, green, blue]];

# â•”â•â•¡ 4149ce52-2e86-4c6b-92ca-d66fd9b8348d
@bind K Slider(1:60, show_value=true, default=30)

# â•”â•â•¡ 818de785-c18d-4b35-9a16-4453b7c9ade7
let kernel = centered([1 2 -1; 2 0 -2; -1 -2 1])
	imfilter(load("/tmp/my_earth.png"), kernel)
end

# â•”â•â•¡ 5d477221-6c4a-409a-98aa-4373b7e05bce
run(`rm /tmp/my_earth.png`)

# â•”â•â•¡ c8c3b967-ddb1-406b-8393-3fee26054d93
@code_llvm Normal()(1)

# â•”â•â•¡ c85e44f0-600a-4491-ba95-bf5b38d83578
begin
	"""`D` dimensional `F` (real/complex) vector space of vector type `T`."""
	abstract type VectorSpace{T, D, F<:Number} end

	basis(s::VectorSpace) = s.basis
	basis(s::VectorSpace, i::Integer) = basis(s)[i]

	struct Vect{D, F, V<:VectorSpace{<:Any,D,F}}
		coefs::SVector{D,<:F}
		space::V

		function Vect(x::AbstractVector, s::VectorSpace{T,D,F}) where {T,D,F}
			new{D,F,typeof(s)}(SVector{D,F}(x), s)
		end
	end

	Base.length(v::Vect) = length(v.coefs)
	vec(v::Vect) = sum(c * b for (b, c) in zip(basis(v.space), v.coefs))

	abstract type InnerProdSpace{T, D, F} <: VectorSpace{T, D, F} end

	struct EuclideanSpace{D, F} <: InnerProdSpace{SVector{D,F}, D, F}
		basis::SMatrix{D,D,F}

		function EuclideanSpace{D,F}(basis=I) where {D,F}
			new{D,F}(SMatrix{D,D,F}(basis))
		end
	end

	basis(s::EuclideanSpace) = eachcol(s.basis)

	function space_type(x::AbstractArray{T,N}) where {T<:Number,N}
		EuclideanSpace{size(x, 1), T <: Real ? Float64 : ComplexF64}
	end

	const â„{N} = EuclideanSpace{N, Float64}
	const â„‚{N} = EuclideanSpace{N, ComplexF64}

	Vect(coefs::AbstractVector; basis=I) = let
		T = space_type(basis isa AbstractArray ? basis : coefs)
		Vect(coefs, T(basis))
	end
	
	VectorSpace(basis::AbstractMatrix) = space_type(basis)(basis)

	Base.in(x::Vect, V::Type{<:VectorSpace}) = x.space isa V

	function Base.getindex(s::VectorSpace, x::T...) where {T}
		Vect(T <: AbstractVector && length(x) == 1 ? x[1] : collect(x), s)
	end

	function Base.:+(u::Vect{D,<:Number,V}, v::Vect{D,<:Number,V}) where {D,V}
		if u.space == v.space
			Vect(u.coefs .+ v.coefs, u.space)
		else
			vec(u) + vec(v)
		end
	end
	
	Base.:-(v::Vect) = Vect(-v.coefs, v.space)
	Base.:*(a::Number, v::Vect) = Vect(a.*v.coefs, v.space)
	Base.:*(x::Vect, y::Number) = y * x
	Base.:-(x, y) = x + (-y)
	Base.:/(x, y::Number) = (1/y) * x
	
	proj(v::Vect, s::VectorSpace) = proj(vec(v), s)

	function proj(x::T, s::VectorSpace{T,D,F}) where {T,D,F}
		B = basis(s)
		if !(x isa AbstractVector) && s isa InnerProdSpace
			x = [x â‹… v for v in B]
			B = [u â‹… v for u in B, v in B]
		end
		Vect(B \ x, s)
	end
end

# â•”â•â•¡ bd367484-2cd2-43b4-9ecf-61bff5511614
function Statistics.mean(A::Array, dims::Integer...)
	if length(dims) == 0
		return sum(A) / length(A)
	end
	for i in sort(collect(dims), rev=true)
		A = sum(A, dims=i) ./ size(A, i)
	end
	return A
end

# â•”â•â•¡ 7a2baa7c-af89-478f-99ee-f5e9fca9f871
begin
	Base.adjoint(f::Function) = x -> ForwardDiff.derivative(f, x)
	sin'(0), cos'(Ï€/2)
end

# â•”â•â•¡ e96c765f-5f5f-41db-8191-1a0d444bcde2
begin
	struct Func{L, H}
		exp::Sym
		var::Sym
	end

	struct FuncSpace{D, F, L, H} <: InnerProdSpace{Sym, D, F}
		basis::SVector{D,Sym}
		var::Sym

		function FuncSpace(basis::Vector{<:Sym}, (lo, hi), field=Real)
			var = only(free_symbols(basis))
			new{length(basis),field,lo,hi}(basis, var)
		end
	
		function FuncSpace(basis::Vector{Func{L,H}}, field=Real) where {L,H}
			var = only(Set(f.var for f in basis))
			new{length(basis),field,L,H}([f.exp for f in basis], var)
		end
	end

	basis(s::FuncSpace{D,F,L,H}) where {D,F,L,H} = Func{L,H}.(s.basis, s.var)
	vec(v::Vect{D,F,<:FuncSpace}) where {D,F} = sum(v.coefs .* basis(v.space)).exp
	(v::Vect{D,F,FuncSpace})(x) where {D,F} = vec(v)(x)

	function PolySpace(deg, range=(0, 1), field=Real, var=:x)
		basis = symbols(var, real=true) .^ (0:deg)
		FuncSpace(basis, range, field)
	end

	function FourierSpace(deg, range=(0, 2Ï€), field=Real, var=:x)
		a, b = range
		T = b - a
		t = symbols(var, real=true) - a
		basis = [exp(-im*PI*k*t/T) for k = 0:deg]
		basis = field <: Real ? real.(basis) : basis
		FuncSpace(basis, range, field)
	end

	Base.:*(a, f::T) where {T<:Func} = T(a * f.exp, f.var)
	Base.:+(f::T, g::T) where {T<:Func} = T(f.exp + g.exp, f.var)
	Base.:-(f::T) where {T<:Func} = T(-f.exp, f.var)

	proj(f::Func, s::FuncSpace) = proj(f.exp, s)
	proj(f::Function, s::FuncSpace) = proj(f(s.var), s)
end

# â•”â•â•¡ f1f274f1-3392-4fff-8e4c-90a4259ef7f0
1 + 2 * 3

# â•”â•â•¡ 6a624939-5e57-450c-b574-7927ffe23ec4
z = exp(im * Ï€)

# â•”â•â•¡ 09f83f22-e305-4d21-a984-10bdf3b458cd
z == -1, z â‰ˆ -1  # tuple

# â•”â•â•¡ 96f1a1a6-7a15-4f8d-81ed-2f6785c15aa8
angle(z)

# â•”â•â•¡ 001d5569-c199-4758-875b-f1057466476d
M = [i + j*im for i in 1:3, j in 1:3]

# â•”â•â•¡ f8e9f8c6-99e7-4919-957c-85608a012e83
M', transpose(M)

# â•”â•â•¡ 7c75d3c1-7757-4afb-a3fc-980798869633
M ^ 2, exp(M)

# â•”â•â•¡ fb605635-8083-4448-b20c-522a45717e3b
rank(M), tr(M), det(M), diag(M)

# â•”â•â•¡ d49583e5-0e8b-44d7-9b4d-25d8e732446b
let b = [3, 2, 1]
	x = @show M \ b  # inv(M) * b
	M * x
end

# â•”â•â•¡ 32342cf4-1149-4443-b257-3da55837d3ba
let eig = eigen(M)
	@show eig.values
	@show eig.vectors
	Î», V = eig
	M * V â‰ˆ Î»' .* V
end

# â•”â•â•¡ 47bb0f8b-899d-485b-ba2e-3e2a7a99a1de
let factorial(n) = n < 2 ? big(1) : n * factorial(n-1)
	@show factorial(32)
	@time factorial.(0:32)
end

# â•”â•â•¡ eba461a3-2026-47c8-8a4a-78dbf8975a3c
begin
	sq(x) = x ^ 2
	double(f) = x -> f(f(x))  # anonymous function
	@show map(double(sq), [3, "3"])
	triple(f) = f âˆ˜ f âˆ˜ f
	inc = Base.Fix1(+, 1)  # inc = x -> 1 + x
	@show triple(double)(inc)(0)  # applies inc for 2^3 times
	nfold(f, n) = foldr(âˆ˜, fill(f, n))
	nfold(triple, 3)(cos)
end

# â•”â•â•¡ 4b2e7984-023e-49f2-83b3-3cc01819e239
fib(n) = n < 2 ? big(n) : fib(n-2) + fib(n-1)

# â•”â•â•¡ c56a1f71-ecdb-41ef-a614-379b32a270db
let f(g) = n -> n < 2 ? n : g(n-1) + g(n-2)
	partial_fib(i) = nfold(f, i)(x -> NaN)
	for i in 1:8
		println(partial_fib(i).(1:8))
	end
	Y_fib = (x -> f(y -> x(x)(y)))(x -> f(y -> x(x)(y)))  # Y combinator
	Y_fib.(1:8)  # f(f(f(f(...))))
end

# â•”â•â•¡ 768ea165-199f-4106-bcab-d476ebf7dea6
function prime_sieve(n)
	mask = trues(n)
	mask[1] = false
	m = Int(floor(sqrt(n)))
	for i in 2:m
		if mask[i]
			mask[2i:i:end] .= false
		end
	end
	findall(mask)
end

# â•”â•â•¡ 78ed1373-b51c-43c2-9227-9d9785848d69
function factorize(n)
	n <= 0 && throw(DomainError("cannot factorize nonpositive integer"))
	factors = Dict()
	for p in prime_sieve(n)
		while n % p == 0
			n Ã·= p
			factors[p] = get(factors, p, 0) + 1
		end
		if n <= 1
			break
		end
	end
	return factors
end

# â•”â•â•¡ 9865e00a-0c32-419e-a8cc-0b4cb35b7031
lucas_lehmer(n, m) = n == 0 ? 4 : (lucas_lehmer(n-1, m)^2 - 2) % m

# â•”â•â•¡ a306cd12-7218-4767-8650-7034dcb6e303
function perfect_numbers(N)
	primes = prime_sieve(N)
	mersennes = big(1) .<< primes .- 1
	[big(2)^(p-1) * m for (p, m) in zip(primes, mersennes)
	 if p < 3 || lucas_lehmer(p-2, m) == 0]
end

# â•”â•â•¡ 9f457e6d-ce3e-41fa-b3ce-eb3c4993286e
perfect_numbers(100)

# â•”â•â•¡ 11d02c6a-a28b-4b6e-9b2a-83ea20e01c28
function is_perfect_number(n)
	pfs, degs = zip(pairs(factorize(n))...)
	factors = []
	for ds in Iterators.product([0:d for d in degs]...)
		push!(factors, prod(p ^ d for (p, d) in zip(pfs, ds)))
	end
	sum(factors) == 2n
end

# â•”â•â•¡ 64ee1549-b481-49a1-88d6-1961b9b82a91
[(n, is_perfect_number(n)) for n in perfect_numbers(15)]

# â•”â•â•¡ fe402f42-0d4a-46fe-ad6d-57eb578d2cf4
begin
	function newton_method(f, x; max_iter=10000, tol=1e-12, buf=nothing)
		for _ in 1:max_iter
			dx = f(x) / f'(x)
			x -= dx
			if buf != nothing
				push!(buf, x)
			end
			if abs(dx) < tol
				return x
			end
		end
		@warn "root not found after $max_iter iterations"
	end
	@time newton_method(sin, 2)
end

# â•”â•â•¡ cc982bfb-8147-4e69-be5c-c75c053e8e33
let f = sin, x = range(-1, 5, 1000)
	y = f.(x)
	x0 = 2
	xs = []
	newton_method(f, x0, max_iter=30, buf=xs)
	@gif for x1 in xs
		plot(x, y, label="f(x)", title="Newton's method")
		plot!([x0, x1], [f(x0), 0], label="tagent line", markershape=:circle)
		plot!([x1, x1], [0, f(x1)], label=nothing, linestyle=:dash)
		annotate!(x0, f(x0), ("x = $(Float16(x0))", 8, :top))
		x0 = x1
	end fps=2
end

# â•”â•â•¡ 672ee9e6-1928-4eab-b2d9-b34179cf99fc
begin
	data = map(SVD_results) do (U, Î£, V)
		U_K = U[:, 1:K]
		Î£_K = Diagonal(Î£[1:K])
		V_K = V[:, 1:K]
		U_K * Î£_K * V_K'
	end
	hcat(img, RGB.(data...))
end

# â•”â•â•¡ 8515d621-6221-4711-a486-0b9c3d3f6be4
function transform_image(img::AbstractMatrix{<:RGB}, basis::Matrix{<:Real})
	M, N = size(img)
	# A = OffsetMatrix(img, -MÃ·2, -NÃ·2)
	A = centered(img)
	I, J = convert.(UnitRange, axes(A))
	sx, sy = svd(basis).S
	P = Iterators.product((I.start*sy):(I.stop*sy), (J.start*sx):(J.stop*sx))
	Idx = Int32.(floor.(inv(basis) * reshape(collect(Iterators.flatten(P)), 2, :)))
	blank = RGB(0, 0, 0)
	color(i, j) = i in I && j in J ? A[i,j] : blank
	C = color.(eachrow(Idx)...)
	reshape(C, length.(P.iterators)...)
end

# â•”â•â•¡ f057d9db-8c17-49a9-a910-c6c9553e9c93
struct Normal
	Î¼ :: Float64  # try removing the type declarations
	Ïƒ :: Float64

	Normal(Î¼=0.0, Ïƒ=1.0) = new(Î¼, Ïƒ)

	(p::Normal)(x) = exp(-0.5((x-p.Î¼)/p.Ïƒ)^2) / (p.Ïƒ * âˆš2Ï€)
end

# â•”â•â•¡ bbf11fd6-4a18-4f4e-9022-6801ec387df9
let p = Normal()
	@code_warntype p(1)
end

# â•”â•â•¡ d3d0a9d6-4540-4d87-9254-489a9335a322
begin
	Base.rand(P::Normal, dims::Integer...) = randn(dims...) .* P.Ïƒ .+ P.Î¼
	Statistics.mean(P::Normal) = P.Î¼
	Statistics.std(P::Normal) = P.Ïƒ
	Statistics.var(P::Normal) = P.Ïƒ ^ 2
end

# â•”â•â•¡ 07d671cb-179f-413a-be83-8d2a0628aa22
let Î¸ = rand(), z = exp(im * Î¸)  # bind variables locally
	x, y = @show reim(z)
	x ^ 2 + y ^ 2 == abs(z) ^ 2
end

# â•”â•â•¡ d4d25d4f-18e6-4a38-8589-3aa7eb47f745
A = rand(Float64, (3, 4))

# â•”â•â•¡ 7888f53a-56d7-4b50-a79c-bd8e472a0938
size(A), size(A, 1)

# â•”â•â•¡ 087e605d-6741-4b58-bdb3-c4d12fd82869
[A[:, 3:4]; A[[1,3], 1:2:end]]  # concat vertically

# â•”â•â•¡ 0a68a4ac-f0e7-43fe-9e39-11dbf007bc7e
[sum(A .^ 2, dims=2) maximum(A, dims=2)]  # concat horizontally

# â•”â•â•¡ 6c63086a-dc7a-485e-95d6-acc5ef8d700b
diff(cumsum(A, dims=2), dims=2) â‰ˆ A[:, 2:end]

# â•”â•â•¡ 053cddd8-48ef-4fba-a510-3ffcce200ea5
let B = reshape(A, 2, 6)
	B[2, 3] = -999
	i = @show findfirst(A .== -999)
	C = @view B[1:2, 2:3]
	A[i] = -1
	C
end

# â•”â•â•¡ 3e2836cd-f94f-41e9-8151-62bfff3af303
mean(M), mean(M, 1), mean(M, 2), mean(M, 1, 2)

# â•”â•â•¡ db5d41fc-ea93-4abc-9efa-b232ef7f37e2
function estimate_pi_mc(n=100_000_000)
	mean(1:n) do _
		rand()^2 + rand()^2 < 1
	end / n * 4
end

# â•”â•â•¡ 175d7472-4a23-4715-a39e-4a434cac46b1
let N = 300_000_000, K = 120
	times = [Float64[] for _ in 1:Threads.nthreads()]
	@time let
		A = ones(K)
		@Threads.threads for i in 1:K
			t0 = time()
			A[i] = prod((i Ã· 2 * 2) / ((i-1) Ã· 2 * 2 + 1) for i in 1+i:K:N)
			push!(times[Threads.threadid()], time() - t0)
		end
		@show 2 * prod(A)
	end
	[f(ts) for ts in times, f in [length, mean]]
end

# â•”â•â•¡ 0c593b1c-e74a-46ed-8f66-47d1456b3636
let p1 = Normal()
	p2 = Normal(-4.0, 0.7)
	@show mean(p1), var(p1), mean(p2), var(p2)
	xs = vcat(rand(p1, 2000), rand(p2, 2000))
	@show mean(xs)
	@show mean((xs .- mean(xs)) .^ 2)
	histogram(xs, label=false, normalize=true, nbin=80)
	let x = range(-10, 10, 1000)
		plot!(x, p1.(x), label="N$((p1.Î¼, p1.Ïƒ))")
		plot!(x, p2.(x), label="N$((p2.Î¼, p2.Ïƒ))")
	end
end

# â•”â•â•¡ 40ae0bd6-0fe5-4537-9cee-9485a8b741f3
methodswith(Vect)

# â•”â•â•¡ 248c304c-de63-4523-b334-e201ed5f07f2
begin
	u = Vect([3, 4])
	v = Vect([3, 4], basis=[1 2; 3 4])
	@show v âˆˆ â„{2}
	u + v
end

# â•”â•â•¡ 2a5009df-5e84-4b50-b819-c19333eb4a8b
let
	f(x) = cos(2x^2)
	g = @show vec(proj(f, p1))
	h = @show vec(proj(f, p2))
	xs = -2.2:0.001:2.2
	plot(f, xs, label="function", legend = :outertopright)
	plot!(g, xs, label="polynomial")
	plot!(h, xs, label="fourier")
end

# â•”â•â•¡ 3f880785-c0ef-447c-b5ee-db258fc9db16
"Linear recurrence of the form ``x_{n+D} = \\sum_{k=0}^{D-1} c_k x_{n+k}``"
struct LinearRecurrence{D} <: VectorSpace{Vector, D, Float64}
	coefs::SVector{D, <:Number}
	roots::SVector{D, <:Number}  # roots of characteristic polynomial

	function LinearRecurrence(coefs...)
		D = length(coefs)
		v = collect(coefs)
		m = vcat(v', I(D)[1:end-1, :])
		new{D}(v, eigvals(m))
	end
end

# â•”â•â•¡ 6b847bf5-87df-4e87-8b1c-fb9da5da0d54
basis(lr::LinearRecurrence{D}) where {D} = hcat([r .^ (1:D) for r in lr.roots]...)

# â•”â•â•¡ 8e4eb2c7-20d3-4aa0-8c09-34fdb8293a9c
begin
	import LinearAlgebra: dot  # to overload the 'â‹…' operator

	dot(A::StaticMatrix, x::StaticVector) = A * x
	dot(u::Vect, v::Vect) = 
		sum(a * (u â‹… x) for (a, x) in zip(v.coefs, basis(v.space)))
	dot(u::Vect, x) = sum(a * (x â‹… y) for (a, y) in zip(u.coefs, basis(u.space)))
	dot(f::Func, g::Func) = dot(f, g.exp)
	dot(f::Func{L,H}, g) where {L,H} =
		integrate(SymPy.simplify(real(f.exp * conj(g))), (f.var, L, H))
	dot(x, u::Union{Vect, Func}) = dot(u, x)

	norm(v) = sqrt(v â‹… v)
	proj(u, v) = (u â‹… v) / (v â‹… v) * v

	function orthogonalize(s::V) where {T,D,F,V<:InnerProdSpace{T,D,F}}
		new_basis = [basis(s, 1)]
		for i in 2:D
			u = basis(s, i)
			push!(new_basis, u - sum(proj(u, v) for v in new_basis))
		end
		new_basis ./ norm.(new_basis)
	end
end

# â•”â•â•¡ 0462c099-18ee-415d-b06a-7bb8eafa360f
let
	m = hcat(orthogonalize(v.space)...)
	w = Vect([3, -4], basis=@show m)
	norm(w)
end

# â•”â•â•¡ 331ceecb-53e4-41f9-b8de-2aece6d1eea8
begin
	p1 = @show PolySpace(6, (-2, 2))
	v1 = p1[1:7]
	s1 = FuncSpace(orthogonalize(p1))
	u1 = proj(v1, s1)
	@show vec(u1).evalf()
	u1
end

# â•”â•â•¡ 25319673-99ff-436d-bf5d-34c540f4faf1
begin
	p2 = @show FourierSpace(6, (-2, 2))
	v2 = p2[0, 1, -1, 2, -2, 3, -3]
	s2 = FuncSpace(orthogonalize(p2))
	u2 = proj(v2, s2)
	@show vec(u2).evalf()
	u2
end

# â•”â•â•¡ 9510e0c5-c658-4358-871f-3c433a8f183d
begin
	Base.getindex(x::Vect{<:Any,<:Number,<:LinearRecurrence}, i::Integer) = 
		(x.space.roots .^ i) â‹… x.coefs
	Base.getindex(x::Vect{<:Any,<:Number,<:LinearRecurrence}, i) = (j -> x[j]).(i)
end

# â•”â•â•¡ 907a813d-945d-40b2-9262-cc2975b8a436
let R = LinearRecurrence(1, 1)  # x[n+2] = x[n+1] + x[n]
	@show basis(R)
	x = R[1, 1]
	@show x[1:5]
	fib = proj([1, 1], R)
	@show fib.coefs
	@time fib[1:32]
end

# â•”â•â•¡ 2eded938-c85c-4bf4-95aa-8d8b02c6cbbb
"Solutions of homogeneous ODE of the form ``\\sum_{k=0}^Dc_k\\frac{d^k}{dt^k}x(t)=0``"
struct ODESolutions{D} <: VectorSpace{Vector, D, Float64}
	eq::Vector
	basis::Vector
	var::Sym

	function ODESolutions(coefs...; var=symbols(:t, real=true))
		v = collect(coefs)
		ord = length(v) - 1
		eq = dot(v, var .^ (0:ord))
		new{ord}(v, exp.(solve(eq, var) .* var), var)
	end
end

# â•”â•â•¡ fb1ad4f2-5b02-4080-bb0f-94a1bf918562
function Base.getindex(ode::ODESolutions, inivals::Pair...)
	ts, xs = collect.(zip(inivals...))
	A = [subs(b, ode.var => t) for t in ts, b in basis(ode)]
	ode[A \ xs]
end

# â•”â•â•¡ 0e8fcc58-1f93-48ef-9c74-f5abdcdecbd3
md"""
# Julia Fundamentals
"""

# â•”â•â•¡ 6cfc8e81-4fe5-4555-870e-427167e68ab1
md"## Types and Fields"

# â•”â•â•¡ f16eab5d-994b-4e94-9d51-acc52021ccd5
md"### Arrays"

# â•”â•â•¡ 05aaecbc-e5b2-43ce-9bf3-e6aef1a3860f
md"## Functions"

# â•”â•â•¡ be46feb4-0e24-4b35-b9ed-c07c776f5424
md"### Some logic"

# â•”â•â•¡ 8fc20559-3ebe-478a-a96a-d0b2f9ca7e6a
md"### Higher order functions"

# â•”â•â•¡ 64b53133-51e6-400e-9047-c2c18d1805f5
md"# Case Study: Fibonacci Sequence"

# â•”â•â•¡ fb1c8a2f-babb-4b93-be81-a1fe843fbfa5
md"Recursion as fixed point of higher order function."

# â•”â•â•¡ b14c1634-2b8b-4ad3-ad3a-2ea44c071471
md"A faster option: calling C function."

# â•”â•â•¡ d66efb8a-03f5-4879-9ae4-5242d8288d67
md"# Case Study: Perfect Numbers"

# â•”â•â•¡ 768bfdcc-11b6-4fd9-9b0b-42035ef89664
md"""
!!! danger "Task"
	Implement prime factorization
"""

# â•”â•â•¡ 4121fd34-e7ae-4a04-a32f-69660e3b85ca
md"Euclid-Euler Theorem"

# â•”â•â•¡ f646fd23-beeb-48d8-80d2-503442371d83
md"""
!!! danger "Task"
	Implement perfect number test
"""

# â•”â•â•¡ 078dbda5-3eec-4c2b-9c74-1349511cdb82
md"# Case Study: Estimate Ï€"

# â•”â•â•¡ 55d77b19-e600-4ce8-9dc1-8d2458c99da2
md"Estimate Ï€ using Monte Carlo"

# â•”â•â•¡ e47e0b0a-377a-451e-93f8-d4430706ef19
md"""Estimate Ï€ using

$$Ï€ = \frac{2\cdot2\cdot4\cdot4\cdot6\cdot6\ldots}
		   {1\cdot3\cdot3\cdot5\cdot5\cdot7\ldots}$$
"""

# â•”â•â•¡ 7f0583b7-8d5c-4660-ad47-5f64aa6f57bb
md"""Estimate Ï€ using Newton's method:

``\pi`` is a root of ``sin(x)``.
"""

# â•”â•â•¡ 36305f0a-a02d-4ba0-8a4b-12a645a6cf39
md"Estimate Ï€ using fixed point iteration:

``f(x), f(f(x)), \ldots`` converges to a fixed point ``x_0`` of ``f`` , i.e. ``f(x_0) = x_0``. 

``\pi`` is a fixed point of ``sin(x) + x``, since ``sin(\pi) + \pi = \pi``."

# â•”â•â•¡ 89143850-3a09-4407-abf6-066087d180ef
md"""Estimate Ï€ using continued fraction ([source](https://en.wikipedia.org/wiki/Euler%27s_continued_fraction_formula#A_continued_fraction_for_%CF%80
)):

`` \pi = \frac{4}{1+\frac{1^2}{2+\frac{3^2}{2+\frac{5^2}{2+\ldots}}}} ``

"""

# â•”â•â•¡ 521a5ff0-44b6-4065-8fb6-6dd3c1b1689a
md"""
!!! danger "Task"
	Estimate Ï€ using the formula above
"""

# â•”â•â•¡ 551603af-23fc-49b6-a3c0-f15c199452dc
md"""
# Case Study: Image Transformation
"""

# â•”â•â•¡ aae002c8-7160-40f2-a01b-dc1192dbd6d2
@bind T PlutoUI.combine() do Child
	Î¸ = Child("Î¸", Slider(0:5:360, show_value=true))
	Ï• = Child("Ï•", Slider(0:5:360, show_value=true))
	x = Child("x", Slider(0.1:0.02:1, show_value=true, default=0.5))
	y = Child("y", Slider(0.1:0.02:1, show_value=true, default=0.5))
	md"""
	1. rotation: $Î¸
	1. horizontal scale: $x
	1. vertical scale: $y
	1. rotation: $Ï•
	"""
end

# â•”â•â•¡ 8a565363-d3bb-423d-ab43-642f781925e4
begin
	rotate(Î¸) = [cos(Î¸) -sin(Î¸); sin(Î¸) cos(Î¸)]
	scaley(a) = [a 0; 0 1]
	scalex(a) = [1 0; 0 a]

	trans = rotate(-T.Ï• * Ï€/180) * scalex(T.x) * scaley(T.y) * rotate(T.Î¸ * Ï€/180)
	img2 = transform_image(img, trans)
end

# â•”â•â•¡ af195cbb-058e-429f-a2a8-0635b827d54f
save("/tmp/my_earth.png", img2)

# â•”â•â•¡ a1eaec8f-3527-43f6-b3e6-360c64b156e5
md"""# Case Study: Normal Distribution"""

# â•”â•â•¡ dafb5973-8bf9-4436-b7e2-cd9fe29e1d2c
md"Try adding and removing the type declarations in `Normal` to see the difference of generated bitcodes."

# â•”â•â•¡ b3ab790a-e748-4634-816a-ed7acd3f31d3
md"# Case Study: Vector Space"

# â•”â•â•¡ cd139cca-0b06-40da-b2b7-f49e3b3bcd52
let ode = ODESolutions(1, 2, -3)  # x + 2x' - 3x'' = 0
	x = vec(ode[0=>2, 1=>1])
	D = SymPy.Differential(ode.var)
	@show x + 2D(x) - 3(D^2)(x)
	x(0).evalf(), x(1).evalf()
end

# â•”â•â•¡ 113a69c4-ada0-4e64-b910-64434f191a7e
md"# Case Study: Energy Balance Model"

# â•”â•â•¡ 479ddea4-9370-408e-8000-1bf02d873a02
begin
	# https://dataframes.juliadata.org/stable/man/comparisons/
	CO2_historical_path = download("https://www.epa.gov/system/files/other-files/2022-07/ghg-concentrations_fig-1.csv")
	# CO2_historical_path = "/home/tcai/Downloads/total-ghg-emissions.csv"

	offset = findfirst(startswith("Year"), readlines(CO2_historical_path))

	CO2_historical_data_raw = CSV.read(
		CO2_historical_path, DataFrame; 
		header=offset, 
		skipto=offset+2,
	)

	last(CO2_historical_data_raw, 220)
end

# â•”â•â•¡ b484e82c-9960-43e2-b90b-f08dfd36214e
begin
	CO2_historical_data = subset(CO2_historical_data_raw, "Year" => y -> y .>= 1850)
	values = replace(Matrix(CO2_historical_data[:,2:end]), missing=>NaN)
	CO2_historical_data.CO2 = reshape(nanmean(values, dims=2), :)
	select!(CO2_historical_data, :Year, :CO2)
	first(CO2_historical_data, 5), last(CO2_historical_data, 5)
end

# â•”â•â•¡ d5ef8924-6ad4-4aeb-9a62-387e1274de66
# Task 1: fit a polynomial to the Keeling curve
begin
	CO2(t) = features(t .- 1850) * CO2_params
	features(t) = hcat(ones(length(t)), t.^3)
	CO2_params = let
		t = CO2_historical_data[:, "Year"] .- 1850
		y = CO2_historical_data[:, "CO2"]
		X = features(t)
		p = X \ y
	end
end

# â•”â•â•¡ 94038e3a-3d01-439c-a01c-2c7aacf16147
begin
	years = 1850:2030
	let df = CO2_historical_data
		plot(df[:, "Year"] , df[:, "CO2"], 
			 label="Global atmospheric COâ‚‚ concentration")
		plot!(years, CO2(years), label="Fitted curve", legend=:bottomright)
	end
	title!("COâ‚‚ observations and fit")
end

# â•”â•â•¡ f2678b18-3819-421a-9b66-b698e132a1d9
begin
	@ModelingToolkit.parameters t Î± a S Î² Î³ C
	@ModelingToolkit.variables Y(t) RC(t)

	absorbed_solar_radiation = (1 - Î±) * S / 4
	outgoing_thermal_radiation = Î² - Î³ * Y
	greenhouse_effect = a * log(RC)

	D = Differential(t)
	eqs = [
		C * D(Y) ~ 
			absorbed_solar_radiation - 
			outgoing_thermal_radiation + 
			greenhouse_effect,
		RC ~ CO2(t+1850) / CO2(1850)
		# RC ~ 1 + (t/220)^3
	]
end

# â•”â•â•¡ ce55e76c-f309-4f34-a8fc-2db2af7d030a
@mtkbuild sys = ODESystem(eqs, t)

# â•”â•â•¡ 17b7c372-269c-44b8-8d7b-048ce10e64f6
begin
	ini = [Y => 14.0]  # initial condition
	ps = [  # parameters
		a => 5.0, 
		Î± => 0.3, 
		C => 51, 
		S => 1368, 
		Î² => 221.2, 
		Î³ => -1.3,
	]
	tspan = (0, 2024-1850)
	prob = ODEProblem(sys, ini, tspan, ps)
end

# â•”â•â•¡ abdf8da8-be19-4428-9230-8bb46f8d03e5
begin
	temps = vcat(solve(prob).(30:180)...)
	plot(1880:2030, temps, lw=2, legend=:topleft,
		 label="Predicted Temperature from model")
	xlabel!("year")
	ylabel!("Temp Â°C")
end

# â•”â•â•¡ cfd3c52f-8b2c-48ab-86f3-2b8835abddd0
begin
	T_url = "https://data.giss.nasa.gov/gistemp/graphs/graph_data/Global_Mean_Estimates_based_on_Land_and_Ocean_Data/graph.txt"
	s = read(download(T_url), String)
	io = replace(s, r" +" => " ") |> IOBuffer
	T_df = CSV.read(io, DataFrame, header=false, skipto=6);
	T_df = rename(T_df[:,1:2], :Column1=>:year, :Column2=>:temp)
	T_df.temp .+= 14.15
	T_df
end

# â•”â•â•¡ ae59ec71-6362-4dd9-bfc7-74fb8c52f777
plot!(T_df[:, :year], T_df[:, :temp], 
	  color=:black, label="NASA Observations", legend=:topleft)

# â•”â•â•¡ d28ba6a0-e1b1-43ca-8744-976da374c98d
md"""The reason why the predicted temperature is lower than the observation is probably that we have not taken into account other greenhouse gases and feedback factors such as water vapour."""

# â•”â•â•¡ 6f9c7c9c-5992-49bb-8090-98a03203414a
# begin
# 	using DiffEqFlux
	
# 	function neural_ode(t, data_dim; saveat = t)
# 	  f = FastChain(FastDense(data_dim, 64, swish),
# 					FastDense(64, 32, swish),
# 					FastDense(32, data_dim))
	
# 	  node = NeuralODE(f, (minimum(t), maximum(t)), Tsit5(),
# 					   saveat = saveat, abstol = 1e-9,
# 					   reltol = 1e-9)
# 	end
# end

# â•”â•â•¡ 37008e44-c0fc-42e9-851a-c4e4a3a522d5


# â•”â•â•¡ 9e4e4f8d-1d34-4c35-b814-b8d6708bf2ab
macro show_all(block)
	if block.head == Symbol("let")
		lines = block.args[2].args
	elseif block.head == Symbol("block")
		lines = block.args
	else
		return block
	end
	foreach(enumerate(lines)) do (i, ex)
		if ex isa Union{Symbol, Expr, Number}
			lines[i] = :(@show $ex)
		end
	end
	return block
end

# â•”â•â•¡ f722fd52-3f0c-4546-90c9-9b885c10a3a5
@show_all begin
	3 + 4; 3 * 4; 3 / 4; 3 Ã· 4; 4 % 3; 3 ^ 4;
end

# â•”â•â•¡ 0a34b4fe-9ca8-42ad-a0f7-a1f29b234ff0
@show_all begin
	log2(4); log(â„¯); log10(1e4); log(4, 1024); sqrt(4); exp(4); cos(0); acos(0);
end

# â•”â•â•¡ 6d3c390d-7e2c-401f-b313-31975874e657
@show_all begin
	typeof(z)
	z.re  # real(z)
	getfield(z, :im)  # imag(z)
	z isa Complex
	z isa Number
end;

# â•”â•â•¡ 9da04702-752f-4bc7-a62f-eaa17d86fa4d
@show_all let T = Complex{Int64}
	T <: Complex
	T <: Number
	T <: Complex{<:Real}
	T <: Complex{Real}
	Dict(zip(fieldnames(T), fieldtypes(T)))
end

# â•”â•â•¡ 2ba441e8-0d84-4bf4-b25a-cc17a64cb554
@show_all let a = -6:3:6, b = [1, 2, 3]
	length(a)
	reverse(a)
	a .* 2
	a[1:3:end]
	a[end:-2:1]
	maximum(a)  # max(a...)
	abs.(a)
	a .^ 2 |> sum  # sum of squares
	count(iseven, a)
	vcat(a, b)
	zip(a, b) |> collect
	map(-, a, b)
	push!(b, 4, 5)
	deleteat!(b, 1)
	accumulate(*, b)
	foldl(-, b)  # 2 - 3 - 4 - 5 (starting from the left)
	["$i: $n" for (i, n) in enumerate(b) if n % 2 != 0]
end

# â•”â•â•¡ 672fb2ff-5782-4411-85d0-ca83506372c8
begin
	almost(text) = Markdown.MD(Markdown.Admonition("warning", "Almost there!", [text]))
	still_missing(text=md"Replace `missing` with your answer.") = Markdown.MD(Markdown.Admonition("warning", "Here we go!", [text]))
	keep_working(text=md"The answer is not quite right.") = Markdown.MD(Markdown.Admonition("danger", "Keep working on it!", [text]))
	yays = [md"Fantastic!", md"Splendid!", md"Great!", md"Yay â¤", md"Great! ğŸ‰", md"Well done!", md"Keep it up!", md"Good job!", md"Awesome!", md"You got the right answer!", md"Let's move on to the next section."]
	correct(text=rand(yays)) = Markdown.MD(Markdown.Admonition("correct", "Got it!", [text]))
end

# â•”â•â•¡ 04ea591e-bd74-4926-82a6-d6a09f242a71
let
	result = all([
		factorize(66) == Dict(2=>1, 3=>1, 11=>1),
		factorize(1) == Dict(),
		factorize(2) == Dict(2=>1),
		factorize(9) == Dict(3=>2),
	])
	if ismissing(result)
		still_missing()
	elseif isnothing(result)
		keep_working(md"Did you forget to write `return`?")
	elseif result
		correct()
	else
		keep_working()
	end
end

# â•”â•â•¡ Cell order:
# â• â•32fbc94f-710f-435f-91a8-13c07b9671f0
# â•Ÿâ”€0e8fcc58-1f93-48ef-9c74-f5abdcdecbd3
# â• â•f1f274f1-3392-4fff-8e4c-90a4259ef7f0
# â• â•f722fd52-3f0c-4546-90c9-9b885c10a3a5
# â• â•0a34b4fe-9ca8-42ad-a0f7-a1f29b234ff0
# â• â•6a624939-5e57-450c-b574-7927ffe23ec4
# â• â•09f83f22-e305-4d21-a984-10bdf3b458cd
# â• â•96f1a1a6-7a15-4f8d-81ed-2f6785c15aa8
# â• â•07d671cb-179f-413a-be83-8d2a0628aa22
# â•Ÿâ”€6cfc8e81-4fe5-4555-870e-427167e68ab1
# â• â•6d3c390d-7e2c-401f-b313-31975874e657
# â• â•135b50a4-66c1-49c5-9865-63d4311ba694
# â• â•9da04702-752f-4bc7-a62f-eaa17d86fa4d
# â• â•65b87a6f-1edd-4b48-9878-90dc112881e6
# â• â•c147a9be-8ce8-4df8-a1f1-1fc6c05a73cc
# â• â•0858a0ec-bbf0-4d04-8755-f88838272822
# â• â•369536ba-ffdc-49fc-9acf-1572300714be
# â• â•a1f21125-0a64-45cc-833a-801d0539892b
# â•Ÿâ”€f16eab5d-994b-4e94-9d51-acc52021ccd5
# â• â•2ba441e8-0d84-4bf4-b25a-cc17a64cb554
# â• â•6ff63d76-7fd6-47fd-9c0e-8a72967a77a2
# â• â•9fd0358b-47e1-4e19-b446-3f8c91769d1c
# â• â•d4d25d4f-18e6-4a38-8589-3aa7eb47f745
# â• â•7888f53a-56d7-4b50-a79c-bd8e472a0938
# â• â•836b97b4-5350-4d85-bf60-17e52d6f9ef0
# â• â•087e605d-6741-4b58-bdb3-c4d12fd82869
# â• â•0a68a4ac-f0e7-43fe-9e39-11dbf007bc7e
# â• â•6c63086a-dc7a-485e-95d6-acc5ef8d700b
# â• â•053cddd8-48ef-4fba-a510-3ffcce200ea5
# â• â•001d5569-c199-4758-875b-f1057466476d
# â• â•f8e9f8c6-99e7-4919-957c-85608a012e83
# â• â•7c75d3c1-7757-4afb-a3fc-980798869633
# â• â•bd367484-2cd2-43b4-9ecf-61bff5511614
# â• â•3e2836cd-f94f-41e9-8151-62bfff3af303
# â• â•d49583e5-0e8b-44d7-9b4d-25d8e732446b
# â• â•fb605635-8083-4448-b20c-522a45717e3b
# â• â•32342cf4-1149-4443-b257-3da55837d3ba
# â•Ÿâ”€05aaecbc-e5b2-43ce-9bf3-e6aef1a3860f
# â• â•b618ae29-ed57-48fc-8102-1767a51a5b7a
# â• â•b6b5be30-5443-4709-9a24-9608b17f20f2
# â• â•47bb0f8b-899d-485b-ba2e-3e2a7a99a1de
# â• â•eebaac1a-1b99-44e5-b9c0-1a3ed1ea8c0b
# â•Ÿâ”€be46feb4-0e24-4b35-b9ed-c07c776f5424
# â• â•8ab3b64e-e1e4-4cdf-9385-9573ad4d5863
# â•Ÿâ”€8fc20559-3ebe-478a-a96a-d0b2f9ca7e6a
# â• â•eba461a3-2026-47c8-8a4a-78dbf8975a3c
# â•Ÿâ”€64b53133-51e6-400e-9047-c2c18d1805f5
# â• â•4b2e7984-023e-49f2-83b3-3cc01819e239
# â• â•321c78d1-d175-43de-8069-cdf7cedaf20c
# â•Ÿâ”€fb1c8a2f-babb-4b93-be81-a1fe843fbfa5
# â• â•c56a1f71-ecdb-41ef-a614-379b32a270db
# â•Ÿâ”€810898ac-546f-4832-9922-8eb61f154325
# â• â•76b59e6b-b1b9-49e3-8fea-fc8a15238c99
# â• â•1d7dce85-6652-4743-9ecc-46145a00bc70
# â•Ÿâ”€b14c1634-2b8b-4ad3-ad3a-2ea44c071471
# â• â•4585ea88-51e7-42c6-a014-e8097db09695
# â• â•11834ef0-f9a4-49a5-92ff-4ffe2c217912
# â•Ÿâ”€d66efb8a-03f5-4879-9ae4-5242d8288d67
# â• â•768ea165-199f-4106-bcab-d476ebf7dea6
# â•Ÿâ”€768bfdcc-11b6-4fd9-9b0b-42035ef89664
# â• â•78ed1373-b51c-43c2-9227-9d9785848d69
# â•Ÿâ”€04ea591e-bd74-4926-82a6-d6a09f242a71
# â• â•9865e00a-0c32-419e-a8cc-0b4cb35b7031
# â•Ÿâ”€4121fd34-e7ae-4a04-a32f-69660e3b85ca
# â• â•a306cd12-7218-4767-8650-7034dcb6e303
# â• â•9f457e6d-ce3e-41fa-b3ce-eb3c4993286e
# â•Ÿâ”€f646fd23-beeb-48d8-80d2-503442371d83
# â• â•11d02c6a-a28b-4b6e-9b2a-83ea20e01c28
# â• â•64ee1549-b481-49a1-88d6-1961b9b82a91
# â•Ÿâ”€078dbda5-3eec-4c2b-9c74-1349511cdb82
# â•Ÿâ”€55d77b19-e600-4ce8-9dc1-8d2458c99da2
# â• â•db5d41fc-ea93-4abc-9efa-b232ef7f37e2
# â• â•9a379335-9430-4fa2-9b84-15e192ace090
# â• â•34d06f6b-5e8e-4823-98e7-e3707feb528d
# â• â•697828f5-2e3b-4c25-86e0-cc97cfdb8432
# â•Ÿâ”€e47e0b0a-377a-451e-93f8-d4430706ef19
# â• â•57f8919a-aadd-4e3b-9a24-cdca7870caa9
# â• â•b700e215-3ba4-45e7-bea2-89f6f7ff73f7
# â• â•175d7472-4a23-4715-a39e-4a434cac46b1
# â• â•dd861e69-2c98-49e1-9970-f5e898048d04
# â•Ÿâ”€7f0583b7-8d5c-4660-ad47-5f64aa6f57bb
# â• â•7a2baa7c-af89-478f-99ee-f5e9fca9f871
# â• â•fe402f42-0d4a-46fe-ad6d-57eb578d2cf4
# â• â•cc982bfb-8147-4e69-be5c-c75c053e8e33
# â•Ÿâ”€36305f0a-a02d-4ba0-8a4b-12a645a6cf39
# â• â•37396c37-0291-4ce6-92c6-defb547f94f5
# â•Ÿâ”€89143850-3a09-4407-abf6-066087d180ef
# â•Ÿâ”€521a5ff0-44b6-4065-8fb6-6dd3c1b1689a
# â•Ÿâ”€551603af-23fc-49b6-a3c0-f15c199452dc
# â• â•4cd08b52-0848-458a-8b92-9909015d8edd
# â• â•f0edaa1c-cd6d-4722-9d3f-0f6328095c48
# â• â•6b313419-288a-4f20-8453-e61fa463d225
# â• â•4149ce52-2e86-4c6b-92ca-d66fd9b8348d
# â• â•672ee9e6-1928-4eab-b2d9-b34179cf99fc
# â• â•8515d621-6221-4711-a486-0b9c3d3f6be4
# â•Ÿâ”€aae002c8-7160-40f2-a01b-dc1192dbd6d2
# â• â•8a565363-d3bb-423d-ab43-642f781925e4
# â• â•af195cbb-058e-429f-a2a8-0635b827d54f
# â• â•818de785-c18d-4b35-9a16-4453b7c9ade7
# â• â•5d477221-6c4a-409a-98aa-4373b7e05bce
# â•Ÿâ”€a1eaec8f-3527-43f6-b3e6-360c64b156e5
# â• â•f057d9db-8c17-49a9-a910-c6c9553e9c93
# â•Ÿâ”€dafb5973-8bf9-4436-b7e2-cd9fe29e1d2c
# â• â•bbf11fd6-4a18-4f4e-9022-6801ec387df9
# â• â•c8c3b967-ddb1-406b-8393-3fee26054d93
# â• â•d3d0a9d6-4540-4d87-9254-489a9335a322
# â• â•0c593b1c-e74a-46ed-8f66-47d1456b3636
# â•Ÿâ”€b3ab790a-e748-4634-816a-ed7acd3f31d3
# â• â•c85e44f0-600a-4491-ba95-bf5b38d83578
# â• â•e96c765f-5f5f-41db-8191-1a0d444bcde2
# â• â•8e4eb2c7-20d3-4aa0-8c09-34fdb8293a9c
# â• â•40ae0bd6-0fe5-4537-9cee-9485a8b741f3
# â• â•248c304c-de63-4523-b334-e201ed5f07f2
# â• â•0462c099-18ee-415d-b06a-7bb8eafa360f
# â• â•331ceecb-53e4-41f9-b8de-2aece6d1eea8
# â• â•25319673-99ff-436d-bf5d-34c540f4faf1
# â• â•2a5009df-5e84-4b50-b819-c19333eb4a8b
# â• â•3f880785-c0ef-447c-b5ee-db258fc9db16
# â• â•6b847bf5-87df-4e87-8b1c-fb9da5da0d54
# â• â•9510e0c5-c658-4358-871f-3c433a8f183d
# â• â•907a813d-945d-40b2-9262-cc2975b8a436
# â• â•2eded938-c85c-4bf4-95aa-8d8b02c6cbbb
# â• â•fb1ad4f2-5b02-4080-bb0f-94a1bf918562
# â• â•cd139cca-0b06-40da-b2b7-f49e3b3bcd52
# â•Ÿâ”€113a69c4-ada0-4e64-b910-64434f191a7e
# â• â•cd2ebd41-45d3-4dd0-aba9-3f42070663b4
# â• â•479ddea4-9370-408e-8000-1bf02d873a02
# â• â•b484e82c-9960-43e2-b90b-f08dfd36214e
# â• â•d5ef8924-6ad4-4aeb-9a62-387e1274de66
# â• â•94038e3a-3d01-439c-a01c-2c7aacf16147
# â• â•f2678b18-3819-421a-9b66-b698e132a1d9
# â• â•ce55e76c-f309-4f34-a8fc-2db2af7d030a
# â• â•abdf8da8-be19-4428-9230-8bb46f8d03e5
# â• â•17b7c372-269c-44b8-8d7b-048ce10e64f6
# â• â•cfd3c52f-8b2c-48ab-86f3-2b8835abddd0
# â• â•ae59ec71-6362-4dd9-bfc7-74fb8c52f777
# â•Ÿâ”€d28ba6a0-e1b1-43ca-8744-976da374c98d
# â• â•6f9c7c9c-5992-49bb-8090-98a03203414a
# â• â•37008e44-c0fc-42e9-851a-c4e4a3a522d5
# â•Ÿâ”€9e4e4f8d-1d34-4c35-b814-b8d6708bf2ab
# â•Ÿâ”€672fb2ff-5782-4411-85d0-ca83506372c8
